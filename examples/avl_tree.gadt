(** We follow the AVL tree algorithm from OCaml Standard Library, where 
    the branches of a node are allowed to differ in height by at most 2. *)

datatype Avl : type * num
datacons Empty : ∀a. Avl (a, 0)
datacons Node :
  ∀a,k,m,n [k=max(m,n) ∧ 0≤m ∧ 0≤n ∧ n≤m+2 ∧ m≤n+2].
     Avl (a, m) * a * Avl (a, n) * Num (k+1) ⟶ Avl (a, k+1)

datatype LinOrder
datacons LT : LinOrder
datacons EQ : LinOrder
datacons GT : LinOrder
external let compare : ∀a. a → a → LinOrder =
  "fun x y -> let c=Pervasives.compare x y in
              if c<0 then LT else if c=0 then EQ else GT"

let height = function
  | Empty -> 0
  | Node (_, _, _, k) -> k

let create = fun l x r ->
  ematch height l, height r with
  | i, j when j <= i -> Node (l, x, r, i+1)
  | i, j when i <= j -> Node (l, x, r, j+1)

let singleton = fun x -> Node (Empty, x, Empty, 1)

let rotr = efunction
    | Empty, x, r -> assert false
    | Node (ll, lx, lr, hl), x, r ->
      assert num 0 <= height r;
      assert type hl = height r + 3;
      eif height lr <= height ll then
        let r' = create lr x r in
        create ll lx r'
      else
        ematch lr with
        | Empty -> assert false
        | Node (lrl, lrx, lrr, _) ->
          let l' = create ll lx lrl in
          let r' = create lrr x r in
          create l' lrx r'

let rotl = efunction
    | l, x, Empty -> assert false
    | l, x, Node (rl, rx, rr, hr) ->
      assert num 0 <= height l;
      assert type hr = height l + 3;
      eif height rl <= height rr then
        let l' = create l x rl in
        create l' rx rr
      else
        ematch rl with
        | Empty -> assert false
        | Node (rll, rlx, rlr, _) ->
          let l' = create l x rll in
          let r' = create rlr rx rr in
          create l' rlx r'

let rec add = fun x -> efunction
  | Empty -> Node (Empty, x, Empty, 1)
  | Node (l, y, r, h) ->
    ematch compare x y, height l, height r with
    | EQ, _, _ -> Node (l, x, r, h)
    | LT, hl, hr ->
      let l' = add x l in
      eif height l' <= hr+2 then create l' y r else rotr (l', y, r)
    | GT, hl, hr ->
      let r' = add x r in
      eif height r' <= hl+2 then create l y r' else rotl (l, y, r')

let rec mem = fun x -> function
  | Empty -> False
  | Node (l, y, r, _) ->
    match compare x y with
    | LT -> mem x l
    | EQ -> True
    | GT -> mem x r

let rec min_binding = function
  | Empty -> assert false
  | Node (Empty, x, r, _) -> x
  | Node ((Node (_,_,_,_) as l), x, r, _) -> min_binding l

let rec remove_min_binding = efunction
  | Empty -> assert false
  | Node (Empty, x, r, _) -> r
  | Node ((Node (_,_,_,_) as l), x, r, _) ->
    let l' = remove_min_binding l in
    (ematch height l', height r with
     | hl', hr when hl' <= hr+2 && hr <= hl'+2 -> create l' x r
     | hl', hr when hl'+3 <= hr -> rotl (l', x, r))

let merge = efunction
  | Empty, Empty -> Empty
  | Empty, (Node (_,_,_,_) as t) -> t
  | (Node (_,_,_,_) as t), Empty -> t
  | Node (_,_,_,ht1), Node (_,_,_,ht2) when ht1+3 <= ht2 -> assert false
  | Node (_,_,_,ht1), Node (_,_,_,ht2) when ht2+3 <= ht1 -> assert false
  | (Node (_,_,_,_) as t1), (Node (_,_,_,_) as t2) ->
    let x = min_binding t2 in
    let t2' = remove_min_binding t2 in
    (ematch height t1, height t2' with
     | ht1, ht2' when ht1 <= ht2'+2 && ht2' <= ht1+2 -> create t1 x t2'
     | ht1, ht2' when ht2'+3 <= ht1 -> rotr (t1, x, t2'))

let rec remove = fun x -> efunction
  | Empty -> Empty
  | Node (l, y, r, h) ->
    ematch compare x y with
    | EQ -> merge (l, r)
    | LT ->
      let l' = remove x l in
      (ematch height l', height r with
       | hl', hr when hl' <= hr+2 && hr <= hl'+2 -> create l' y r
       | hl', hr when hl'+3 <= hr -> rotl (l', y, r))
    | GT ->
      let r' = remove x r in
      (ematch height l, height r' with
       | hl, hr' when hl <= hr'+2 && hr' <= hl+2 -> create l y r'
       | hl, hr' when hr'+3 <= hl -> rotr (l, y, r'))
